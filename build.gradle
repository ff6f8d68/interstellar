import java.util.zip.ZipEntry
import java.util.zip.ZipFile
import java.util.zip.ZipOutputStream

buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
        maven {
            name 'Curios maven'
            url = "https://maven.theillusivec4.top/"
        }
        maven { url = 'https://maven.blamejared.com/' }
    }

    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'maven-publish'
}

apply plugin: 'org.spongepowered.mixin'

group = mod_group_id
version = mod_version
ext {
    useDevBuild = project.hasProperty('useDevBuild') && project.useDevBuild.toBoolean()
    modVersionBase = mod_version // from gradle.properties
    computedModVersion = modVersionBase
    project.version = computedModVersion
}

if (useDevBuild) {
    tasks.register("computeDevBuildVersion") {
        dependsOn 'jar' // ensure jar exists
        doLast {
            def jarFile = tasks.named("jar").get().archiveFile.get().asFile
            if (!jarFile.exists()) {
                throw new GradleException("Jar file not found: $jarFile")
            }

            // SHA-256 hash
            def hash = java.security.MessageDigest.getInstance("SHA-256")
                    .digest(jarFile.bytes)
                    .encodeHex()
                    .toString()
                    .substring(0, 8)

            computedModVersion = "devbuild-${hash}"
            println "Dev build version: $computedModVersion"

            // Update project.version dynamically so archiveFileName uses it
            version = computedModVersion

            // Rename the jar file itself
            def newJarFile = new File(jarFile.parentFile, "${base.archivesName.get()}-${computedModVersion}.jar")
            jarFile.renameTo(newJarFile)
        }
    }

    build.dependsOn 'computeDevBuildVersion'
} else {
    project.version = modVersionBase
}
base {
    archivesName = mod_id
}
configurations {
    shade
    shadowLibs
    library
    implementation.extendsFrom shadowLibs
    implementation.extendsFrom library
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    modularity.inferModulePath = false
}

minecraft {
    // The mappings can be changed at any time and must be in the following format.
    // Channel:   Version:
    // official   MCVersion             Official field/method names from Mojang mapping files
    // parchment  YYYY.MM.DD-MCVersion  Open community-sourced parameter names and javadocs layered on top of official
    //
    // You must be aware of the Mojang license when using the 'official' or 'parchment' mappings.
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Parchment is an unofficial project maintained by ParchmentMC, separate from MinecraftForge
    // Additional setup is needed to use their mappings: https://parchmentmc.org/docs/getting-started
    //
    // Use non-default mappings at your own risk. They may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: mapping_channel, version: mapping_version

    // When true, this property will have all Eclipse/IntelliJ IDEA run configurations run the "prepareX" task for the given run configuration before launching the game.
    // In most cases, it is not necessary to enable.
    // enableEclipsePrepareRuns = true
    // enableIdeaPrepareRuns = true

    // This property allows configuring Gradle's ProcessResources task(s) to run on IDE output locations before launching the game.
    // It is REQUIRED to be set to true for this template to function.
    // See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
    copyIdeResources = true

    // When true, this property will add the folder name of all declared run configurations to generated IDE run configurations.
    // The folder name can be set on a run configuration using the "folderName" property.
    // By default, the folder name of a run configuration is the name of the Gradle project containing it.
    // generateRunFolders = true

    // This property enables access transformers for use in development.
    // They will be applied to the Minecraft artifact.
    // The access transformer file can be anywhere in the project.
    // However, it must be at "META-INF/accesstransformer.cfg" in the final mod jar to be loaded by Forge.
    // This default location is a best practice to automatically put the file in the right place in the final jar.
    // See https://docs.minecraftforge.net/en/latest/advanced/accesstransformers/ for more information.
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        // applies to all the run configs below
        configureEach {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'


            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'
            mods {
                "valkyrienskies" {
                    file "run/mods/valkyrienskies.jar"
                }
                "curios" {
                    file "run/mods/Curios.jar"
                }
            }
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', mod_id
            property 'mixin.env.remapRefMap', 'true'
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            // example of overriding the workingDirectory set in configureEach above
            workingDirectory project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}
minecraft.runs.all {
    // Set the minecraft_classpath token to the paths of all jars in the library configuration
    // This is added with the actual Minecraft classpath to get the real classpath information later on
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}
mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = "Valkyrien Skies"
        url = 'https://maven.valkyrienskies.org'
        content {
            excludeGroup("net.jodah")
            excludeGroup("org.spongepowered")
        }
    }
    maven {
        name = 'kotlinforforge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
    }

    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name 'Curios maven'
        url = "https://maven.theillusivec4.top/"
    }
    maven { url = 'https://maven.blamejared.com/' }
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html#sub:flat_dir_resolver
    // flatDir {
    //     dir 'libs'
    // }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation "org.valkyrienskies.core:api:$vs_core_version"
    implementation "org.valkyrienskies.core:impl:$vs_core_version"
    implementation "org.valkyrienskies.core:api-game:$vs_core_version"
    implementation "org.valkyrienskies.core:util:$vs_core_version"
    implementation fg.deobf("org.valkyrienskies:valkyrienskies-120-forge:$vs2_version")
    implementation fg.deobf("thedarkcolour:kotlinforforge:$kotlin_version")



    implementation fg.deobf("team.lodestar.lodestone:lodestone:1.20.1-1.4.2.62")
    //1.18.2 Example: implementation fg.deobf("team.lodestar.lodestone:lodestone:1.18.2-1.4.1.435")
    //1.19.2 Example: implementation fg.deobf("team.lodestar.lodestone:lodestone:1.19.2-1.4.2.83")
    //1.20.1 Example: implementation fg.deobf("team.lodestar.lodestone:lodestone:1.20.1-1.4.2.62")

    // === JVM libraries ===

    // === Curios (Forge mod integration) ===
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.14.1+${minecraft_version}:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.14.1+${minecraft_version}")

    // Valkyrien Skies 2 JAR (deobfuscate in dev)




    // Specify the version of Minecraft to use.
    // Any artifact can be supplied so long as it has a "userdev" classifier artifact and is a compatible patcher artifact.
    // The "userdev" classifier will be requested and setup by ForgeGradle.
    // If the group id is "net.minecraft" and the artifact id is one of ["client", "server", "joined"],
    // then special handling is done to allow a setup of a vanilla dependency without the use of an external repository.


    // Example mod dependency with JEI - using fg.deobf() ensures the dependency is remapped to your development mappings
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}")
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}")
    // runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")

    // Example mod dependency using a mod jar from ./libs with a flat dir repository
    // This maps to ./libs/coolmod-${mc_version}-${coolmod_version}.jar
    // The group id is ignored when searching -- in this case, it is "blank"
    // implementation fg.deobf("blank:coolmod-${mc_version}:${coolmod_version}")

    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html


    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.6.1'
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
                             forge_version       : forge_version, forge_version_range: forge_version_range,
                             loader_version_range: loader_version_range,
                             mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
                             mod_authors         : mod_authors, mod_description: mod_description,]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1", // We are version 1 of ourselves
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }

    // This is the preferred method to reobfuscate your jar file
    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

test {
    useJUnitPlatform()
}
shadowJar {
    archiveClassifier.set('shadow')
    configurations = [project.configurations.shadowLibs]

    // relocate for mod compatibility
    relocate 'com.github.stephengold', 'com.noodlegamer76.noodleengine.libs.stephengold'
    relocate 'io.github.electrostat', 'com.noodlegamer76.noodleengine.libs.electrostat'
    relocate 'de.javagl', 'com.noodlegamer76.noodleengine.de.javagl'
}
task moveFiles {
    doLast {
        // Input and output files based on the shadowJar and jar archive file names
        def inputFile = file("$buildDir/libs/" + shadowJar.archiveFileName.get())
        def outputFile = file("$buildDir/libs/" + jar.archiveFileName.get())

        // Temporary file for the updated output
        def tempFile = file("$buildDir/libs/" + "output_temp.zip")

        if (inputFile.exists() && outputFile.exists()) {
            def sourceZip = new ZipFile(inputFile)
            def destinationZip = new ZipFile(outputFile)
            def zipOut = new ZipOutputStream(new FileOutputStream(tempFile))

            // Copy existing contents of the destination ZIP to the temporary file
            destinationZip.entries().each { entry ->
                zipOut.putNextEntry(new ZipEntry(entry.getName()))
                zipOut.write(destinationZip.getInputStream(entry).readAllBytes())
                zipOut.closeEntry()
            }

            // Copy new files from the source ZIP that aren't already in the destination ZIP
            sourceZip.entries().each { entry ->
                if (destinationZip.getEntry(entry.getName()) == null) {
                    zipOut.putNextEntry(new ZipEntry(entry.getName()))
                    zipOut.write(sourceZip.getInputStream(entry).readAllBytes())
                    zipOut.closeEntry()
                } else {
                    println "File already exists in destination ZIP: ${entry.getName()}. Skipping."
                }
            }

            // Close all streams
            zipOut.close()
            sourceZip.close()
            destinationZip.close()

            // Replace the original destination file with the new one
            if (outputFile.exists()) {
                outputFile.delete() // Delete the old output file if it exists
            }
            tempFile.renameTo(outputFile) // Rename temp file to the destination file
            inputFile.delete() // Delete the source file (inputFile) after moving
        } else {
            println "Input or output ZIP file not found."
        }
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.jar) // explicitly publish only the jar task
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ff6f8d68/interstellar")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
build {
    dependsOn shadowJar
    finalizedBy moveFiles
}

tasks.named('processResources', ProcessResources).configure {
    filesMatching('META-INF/mods.toml') {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                MOD_VERSION: useDevBuild ? computedModVersion : mod_version
        ])
    }
}

